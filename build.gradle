apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'docker'
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.gorylenko.gradle-git-properties"


ext {
    scalaMajorVersion = '2.13'
    scala = '2.13.6'
    scalaTestVersion = '3.2.10'
    scalaLoggingVersion = '3.9.4'
    kafkaVersion = '3.0.0'
    avroVersion = '1.11.0'
    slf4jVersion = "1.7.32"
    confluentVersion = '3.2.0'
    pathikritVersion = '3.9.1'
    ftpserverVersion = '1.1.1'
    commonsNetVersion = '3.8.0'
    commonsCodecVersion = '1.15'
    mockitoVersion = '3.2.10.0'
    pegdownVersion = '1.6.0'
}

compileScala {
    scalaCompileOptions.additionalParameters = ["-feature", "-Xexperimental", "-language:implicitConversions"]
}

dependencies {
    compile "org.scala-lang:scala-library:$scala"
    compile "com.typesafe.scala-logging:scala-logging_$scalaMajorVersion:$scalaLoggingVersion"
    compile "org.apache.kafka:connect-api:$kafkaVersion"
    compile "com.github.pathikrit:better-files_$scalaMajorVersion:$pathikritVersion"
    compile "org.apache.ftpserver:ftpserver-core:$ftpserverVersion"
    compile "commons-net:commons-net:$commonsNetVersion"
    compile "commons-codec:commons-codec:$commonsCodecVersion"

    testCompile "org.scalatest:scalatest_$scalaMajorVersion:$scalaTestVersion"
    testCompile "org.scalatestplus:mockito-3-4_$scalaMajorVersion:$mockitoVersion"
    testCompile "com.vladsch.flexmark:flexmark-all:0.62.2"

    testRuntime "org.pegdown:pegdown:$pegdownVersion"
    testRuntime "org.slf4j:slf4j-log4j12:$slf4jVersion"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://packages.confluent.io/maven/" }
    maven { url "http://repo.typesafe.com/typesafe/releases/" }
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.github.maiflai:gradle-scalatest:0.14"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
    }
}

configurations.compile {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()

docker {
    baseImage 'eneco/kafka-connect-base:0.1.0'
    maintainer 'Eneco Dev'
}

shadowJar{
    dependsOn test
}

group = "eneco"
task buildDocker(type: Docker) {
    addFile("build/libs/$project.name-$version-all.jar","/usr/local/share/java/$project.name-$version-all.jar")
    dependsOn shadowJar
}